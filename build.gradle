plugins {
    id "dev.clojurephant.clojure" version "0.5.0"
    id 'org.jetbrains.intellij' version '0.4.18'
    id "org.jetbrains.kotlin.jvm" version "1.3.71"
}

group 'org.illyasviel'
version amethystVersion

repositories {
    mavenCentral()
    maven {
        name = 'Clojars'
        url = 'https://repo.clojars.org/'
    }
}

sourceSets {
    pre
    main.compileClasspath += files(compilePreClojure)
    test.runtimeClasspath += files(compilePreClojure)
}

configurations {
    preImplementation.extendsFrom implementation
}

dependencies {
    implementation 'org.clojure:clojure:1.10.1'
    implementation 'org.clojure:tools.namespace:1.0.0'
    implementation 'refactor-nrepl:refactor-nrepl:2.4.0'
    implementation 'cider:cider-nrepl:0.18.0'
    implementation 'nrepl:nrepl:0.7.0'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version intellijPlatformVersion
    type 'IC'
    downloadSources = false
}

patchPluginXml {
    sinceBuild customSinceBuild
    untilBuild customUntilBuild
}

publishPlugin {
    token publishToken
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    from sourceSets.pre.output
}

clojure.builds {
    pre {
        aotAll()
    }
}
